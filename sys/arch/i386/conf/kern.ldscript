/*	$NetBSD: kern.ldscript,v 1.22 2018/06/24 18:24:53 maxv Exp $	*/

#include "assym.h"

__PAGE_SIZE = 0x1000 ;

ENTRY(_start)
SECTIONS
{
	.text : AT (ADDR(.text) & 0x0fffffff)
	{
		*(.text)
		*(.text.*)
		*(.stub)
		. = ALIGN(__PAGE_SIZE);
	} =0xCC
	_etext = . ;
	PROVIDE (etext = .) ;

	. = ALIGN(__PAGE_SIZE);

	__rodata_start = . ;

	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
	}

	. = ALIGN(__PAGE_SIZE);

	__data_start = . ;
	.data :
	{
		*(.data)
	}

	_edata = . ;
	PROVIDE (edata = .) ;
	__bss_start = . ;
	.bss :
	{
		*(.bss)
		*(.bss.*)
		*(COMMON)
		. = ALIGN(__PAGE_SIZE);
		__bss_end = .;
	}

	. = ALIGN(__PAGE_SIZE);

	/* End of the kernel image */
	__kernel_end = . ;

	_end = . ;
	PROVIDE (end = .) ;
}
