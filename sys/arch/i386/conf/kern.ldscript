/*	$NetBSD: kern.ldscript,v 1.7 2007/10/18 15:28:34 yamt Exp $	*/

/*
 * Kernel linker script for NetBSD/i386.  This script is based on
 * elf_i386.x, but puts _etext after all of the read-only sections.
 */

OUTPUT_FORMAT("elf32-i386", "elf32-i386",
	      "elf32-i386")
OUTPUT_ARCH(i386)
ENTRY(_start)
SEARCH_DIR(/usr/lib);
/* Do we need any of these for elf?
   __DYNAMIC = 0;    */
SECTIONS
{
   /* Read-only sections, merged into text segment: */
   .text :
   AT (ADDR(.text) & 0x0fffffff)
   {
     *(.text)
     *(.text.*)
     *(.stub)
   } =0
   _etext = . ;
   PROVIDE (etext = .) ;

   .rodata :
   AT (LOADADDR(.text) + (ADDR(.rodata) - ADDR(.text)))
   {
     *(.rodata)
     *(.rodata.*)
   }
 
   /* Adjust the address for the data segment.  We want to adjust up to
      the same address within the page on the next page up.  */
   . = ALIGN(0x1000) + (. & (0x1000 - 1));
   __data_start = . ;
   .data :
   AT (LOADADDR(.text) + (ADDR(.data) - ADDR(.text)))
   {
     *(.data)
     *(.data.*)
   }
   _edata = . ;
   PROVIDE (edata = .) ;
   __bss_start = . ;
   .bss :
   AT (LOADADDR(.text) + (ADDR(.bss) - ADDR(.text)))
   {
     *(.bss)
     *(.bss.*)
     *(COMMON)
     . = ALIGN(32 / 8);
   }
   . = ALIGN(32 / 8);
  _end = . ;
  PROVIDE (end = .) ;
  .note.211bsd.ident :
  {
    KEEP(*(.note.211bsd.ident));
  }
}
